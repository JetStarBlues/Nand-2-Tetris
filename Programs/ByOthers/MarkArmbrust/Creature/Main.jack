// Main.jack -- Random walk test using LCGRandom
//
// Copyright (C) 2016, Mark A. Armbrust.  All rights reserved.
// Permission granted for educational use.

class Main {
    static Array dx;
    static Array dy;

    function void main()
    {
        var int i, x, y, r, seed;
        do Main.makeDxy();

        while(true) {
            // Seed LCGRandom with a [hopefully] unique value from the user.
            do Output.moveCursor(0, 0);
            do Output.printString("Hit any key...");
            while (Keyboard.keyPressed() = 0) {
                let seed = seed+1;
            }
            do LCGRandom.setSeed(seed);

            // Draw random walk starting from the center of the screen.
            do Screen.clearScreen();
            let x = 256;
            let y = 128;

            let i = -1;
            while (i < 32767) {
                // Random step: left, right, up or down, wrapping across edges.
                let r = LCGRandom.rand() & 3;
                let x = (x+dx[r]) & 511;
                let y = (y+dy[r]) & 255;
                do Screen.drawPixel(x, y);

                let i = i+1;
            }
        }
        return;
    }

    function void makeDxy()
    {
        let dx = Array.new(4);
        let dy = Array.new(4);
        let dx[0] = 0;  let dy[0] = 1;
        let dx[1] = -1; let dy[1] = 0;
        let dx[2] = 1;  let dy[2] = 0;
        let dx[3] = 0;  let dy[3] = -1;
        return;
    }

}